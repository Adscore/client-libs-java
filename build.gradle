plugins {
    id 'java-library'
    id "com.diffplug.gradle.spotless" version "3.27.0"
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.2'
}

group 'com.adscore'
version '1.0.2'

sourceCompatibility = 1.8
 
repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'junit', name: 'junit', version: '4.12'
    implementation 'com.github.seancfoley:ipaddress:5.2.1'
}

spotless {
    java {
        target fileTree(project.projectDir) {
            include '**/*.java'
            // exclude assertj generated classes
            exclude 'build/generated-src/**/*.*'
        }
        importOrder 'java', 'javax', 'org', 'com', 'co', ''
        removeUnusedImports()

        googleJavaFormat()
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'adscore-client-libraries'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'AdScore.com client libraries'
                description = 'Package containing libraries facilitating using of AdScore.com APIs'
                url = 'https://github.com/Adscore/client-libs-java'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/Adscore/client-libs-java/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'lhyla'
                        name = 'Łukasz Hyła'
                        email = 'lukasz.hyla@iterative.pl'
                    }
                    developer {
                        id = 'kgasior'
                        name = 'Krzysztof Gąsior'
                        email = 'krzysztof.gasior@iterative.pl'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com:Adscore/client-libs-java.git'
                    developerConnection = 'scm:git:ssh://github.com:Adscore/client-libs-java.git'
                    url = 'https://github.com/Adscore/client-libs-java/tree/master'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("ossrhUsername") ?: "none"
                password = project.findProperty("ossrhPassword") ?: "none"
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

nexusStaging {
    packageGroup = "com.adscore"
    username = project.findProperty("ossrhUsername") ?: "none"
    password = project.findProperty("ossrhPassword") ?: "none"
}
